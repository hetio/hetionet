:author: Daniel Himmelstein
:twitter: dhimmel
:tags: Hetionet, hetnet, disease
:neo4j-version: 3.0
:linkattrs:

== Hetionet in Neo4j

++++
<div class="col-lg-3 ng-scope">
++++
=== Hetionet v1.0

Hetionet is a network of biology, disease, and pharmacology. Knowledge from millions of biomedical studies over the last half century have been encoded into a single hetnet. Version 1.0 contains 47,031 nodes of 11 types and 2,250,197 relationships of 24 types.

We created link:https://github.com/dhimmel/hetionet[Hetionet] v1.0 for link:https://thinklab.com/p/rephetio[Project Rephetio] (link:https://doi.org/10.1101/087619[manuscript]) — where we systematically looked at why drugs work and predicted new uses for existing drugs.

++++
</div>
++++

++++
<div class="col-lg-3 ng-scope">
++++

=== Neo4j

Neo4j is a database designed for hetnets (graphs with multiple node or relationship types). You're currently interacting with Hetionet through the Neo4j Browser, which provides a web interface to the database. This is a read-only instance, so you can't modify the network. However, you can run queries and and explore the network.

If you are new to Neo4j, check out the following guides:

 * +++<a play-topic="concepts">Concepts</a>+++
 * +++<a play-topic="cypher">Cypher</a>+++
 * +++<a play-topic="intro">Browser Intro</a>+++

++++
</div>
<div class="col-lg-6 ng-scope">
++++
=== Metagraph

The metagraph shows the data model (schema) for Hetionet v1.0. For an interactive version, run `CALL db.schema()`.

++++
<img src="https://github.com/dhimmel/rephetio/raw/5f834b14b94b9b9d2082c5ae0303b57d634c3a40/figure/metagraph-cypher.png" title="Hetionet v1.0 Metagraph for Cypher" class="img-responsive">

</div>
++++

== Explore Hetionet

++++
<div class="col-lg-3 ng-scope">
++++

=== Label counts

In Neo4j, node types are called labels. The following query counts the number of nodes per label. Run it by clicking the text box to prefill the command and then hitting +++<a tooltip-placement="left" class="circled play sl sl-play"></a>+++ in the upper right.

[source,cypher]
----
MATCH (node)
RETURN
  head(labels(node)) AS label,
  count(*) AS count
ORDER BY count DESC
----
++++
</div>
<div class="col-lg-3 ng-scope">
++++

=== Relationship type counts

Play the following query to count the number of relationships per type.

[source,cypher]
----
MATCH ()-[rel]->()
RETURN
  type(rel) AS rel_type,
  count(*) AS count
ORDER BY count DESC
----

Notice the suffixes (e.g. `GpPW` in `PARTICIPATES_GpPW`) which we include to ensure that relationship types between different labels are distinct.

++++
</div>
<div class="col-lg-6 ng-scope">
++++

=== Random relationships

The following query retrieves a random relationship of each type. The query goes through every relationship and thus may take several seconds.

[source,cypher]
----
MATCH ()-[rel]->()
WITH type(rel) AS rel_type, collect(rel) AS rels
WITH rels[toInteger(rand() * size(rels))] AS rel
RETURN startNode(rel), rel, endNode(rel)
----

Note that by default, the Neo4j Browser draws all relationships between retrieved nodes. Toggle `AUTO-COMPLETE` to `OFF` (located in the bottom right of a frame) to _only_ display queried relationships.

++++
</div>
++++

== Project Rephetio

Project Rephetio uses Hetionet to predict the probability that each compound treats each disease. The approach uses hetnet edge prediction — an algorithm that learns which types of paths occur more frequently between known treatments. Use the link:http://het.io/repurpose/[Prediction Browser, title="Project Rephetio Prediction Browser on het.io"] to browse the predicted probabilities of treatment for 209,168 compound–disease pairs.

Each prediction has a corresponding Neo4j guide, which provides additional details and visualization. For example, play the following commands to see the evidence for

* *bupropion for nicotine dependence* (link:https://thinklab.com/d/203#8[read more]):
+
[source,cypher]
----
:play https://neo4j.het.io/guides/rep/DB01156/DOID_0050742.html
----

* *clofarabine treating multiple sclerosis* (link:https://thinklab.com/d/203#13[read more]):
+
[source,cypher]
----
:play https://neo4j.het.io/guides/rep/DB00631/DOID_2377.html
----

* *nortriptyline treating migraine* (link:https://thinklab.com/d/203#7[read more]):
+
[source,cypher]
----
:play https://neo4j.het.io/guides/rep/DB00540/DOID_6364.html
----

== Learn how to query Hetionet using Cypher

Here are some simple queries to help new users get acquainted with Cypher and Hetionet.

1. *Retrieve the Disease node named lung cancer:*
+
[source,cypher]
----
MATCH (node:Disease {name: "lung cancer"}) RETURN node
----
+
Which is equivalent to:
+
[source,cypher]
----
MATCH (node:Disease)
WHERE node.name = "lung cancer"
RETURN node
----

2. *Find the anatomies (tissue types) where lung cancer localizes:*
+
[source,cypher]
----
MATCH path = (:Disease {name: 'lung cancer'})-[:LOCALIZES_DlA]->()
RETURN path
----

3. *Find all genes associated with spinal cancer:*
+
[source,cypher]
----
MATCH path = (:Disease {name: 'spinal cancer'})-[:ASSOCIATES_DaG]->()
RETURN path
----

4. *Find all genes associated with both liver and kidney cancer* (return results as a table):
+
[source,cypher]
----
MATCH (source:Disease)-[:ASSOCIATES_DaG]-(gene:Gene)-[:ASSOCIATES_DaG]-(target:Disease)
WHERE source.name = 'liver cancer'
  AND target.name = 'kidney cancer'
RETURN
  gene.name AS gene_symbol,
  gene.description AS gene_name,
  gene.url AS url
ORDER BY gene_symbol
----

5. *Find all genes that participate in the mitotic spindle checkpoint biological process:*
+
[source,cypher]
----
MATCH path = ({name: 'mitotic spindle checkpoint'})-[rel:PARTICIPATES_GpBP]-()
RETURN path
----

6. *Find all genes that participate in the mitotic spindle checkpoint and are expressed in the lung:*
+
[source,cypher]
----
MATCH path = (bp:BiologicalProcess)-[:PARTICIPATES_GpBP]-(gene:Gene)-[:EXPRESSES_AeG]-(anatomy:Anatomy)
WHERE bp.name = 'mitotic spindle checkpoint'
  AND anatomy.name = 'lung'
RETURN path
----

For more advanced examples, see our link:https://doi.org/10.15363/thinklab.d220[query depot].

== Miscellany

=== Style

Execute this command to load the hetionet style. Once the style is loaded, the node coloring in the browser will match the metagraph from the first slide in this guide. This command only needs to be run once per web browser.

[source]
----
:style https://neo4j.het.io/guides/graphstyle.grass
----

=== Hosting

link:https://neo4j.het.io[*neo4j.het.io*] is hosted on link:https://www.digitalocean.com/[DigitalOcean] (link:https://doi.org/10.15363/thinklab.d216[learn more]). We'd like to thanks DigitalOcean for their sponsorship of the Hetionet Browser.

== Querying Hetionet from Python

We allow users to programmatically query Hetionet. Our Neo4j instance supports HTTP(S) and Bolt connections. The code below shows how to query Hetionet from Python using the official `neo4j` driver and the `py2neo` community driver.

[source,python]
----
# We use Pandas DataFrames to store tabular query results
# However, this is an optional step for downstream convenience
import pandas

# Return 5 arbitrary diseases
query = '''
MATCH (disease:Disease)
RETURN
  disease.identifier as identifier,
  disease.name AS name
LIMIT 5
'''

# Uses the official neo4j-python-driver. See https://github.com/neo4j/neo4j-python-driver
from neo4j.v1 import GraphDatabase
driver = GraphDatabase.driver("bolt://neo4j.het.io")
with driver.session() as session:
    result = session.run(query)
    result_df = pandas.DataFrame((x.values() for x in result), columns=result.keys())

# Uses py2neo. See http://py2neo.org/v3/
import py2neo
graph = py2neo.Graph("bolt://neo4j.het.io", bolt=True, secure=True,
    http_port=80, https_port=443, bolt_port=7687)
cursor = graph.run(query)
result_df = pandas.DataFrame.from_records(cursor, columns=cursor.keys())
----

In addition to Python, Neo4j has driver support for link:https://neo4j.com/developer/language-guides/[many other languages].

We currently limit queries to 120 seconds. If you notice that the Neo4j server is overloaded, please hold off automated queries. If you are doing a substantial amount of querying, please run the database locally (see the link:https://hub.docker.com/r/dhimmel/hetionet/[Hetionet Docker]).
